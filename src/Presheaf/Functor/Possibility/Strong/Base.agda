{-# OPTIONS --safe --without-K #-}

open import Kripke.IFrame
import Kripke.FDFrame as FDF

module Presheaf.Functor.Possibility.Strong.Base
  {W   : Set}
  {_âŠ†_ : (w w' : W) â†’ Set}
  {IF  : IFrame W _âŠ†_}
  {_R_ : (w v : W) â†’ Set}
  (let open FDF IF _R_)
  {DF  : DFrame}
  (let open Definitions DF)
  (IDF : InclusiveDFrame)
  where

open DFrame DF
open InclusiveDFrame IDF

open import Presheaf.Base IF
open import Presheaf.CartesianClosure IF
open import Presheaf.Functor.Possibility.Base DF

open import Data.Product using (âˆƒ; _Ã—_; _,_; -,_) renaming (projâ‚ to fst; projâ‚‚ to snd)

open import Relation.Binary.PropositionalEquality using (_â‰¡_; refl; sym; trans; subst; cong)
import Relation.Binary.Reasoning.Setoid as EqReasoning

private
  variable
    w w' w'' : W
    ğ’« ğ’«' ğ’¬ ğ’¬' â„› â„›' : Psh

strength[_,_] : (ğ’« ğ’¬ : Psh) â†’ ğ’« Ã—' (â—‡ ğ’¬) â†’Ì‡ â—‡ (ğ’« Ã—' ğ’¬)
strength[ ğ’« , ğ’¬ ] = record
  { fun     = Î» pÃ—â—‡q â†’ strength-fun (Ï€â‚' .apply pÃ—â—‡q) (Ï€â‚‚' .apply pÃ—â—‡q)
  ; pres-â‰‹  = Î» râ‰‹r' â†’ strength-fun-pres-â‰‹ (Ï€â‚' .apply-â‰‹ râ‰‹r') (Ï€â‚‚' .apply-â‰‹ râ‰‹r')
  ; natural = Î» w pÃ—â—‡q â†’ strength-fun-natural w (Ï€â‚' .apply pÃ—â—‡q) (Ï€â‚‚' .apply pÃ—â—‡q)
  }
  where

  strength-fun : ğ’« â‚€ w â†’ â—‡-Fam ğ’¬ w â†’ â—‡-Fam (ğ’« Ã—' ğ’¬) w
  strength-fun p (elem (Î” , r , q)) = elem (Î” , r , elem (wk[ ğ’« ] (R-to-âŠ† r) p , q))

  opaque
    strength-fun-pres-â‰‹ : {p p' : ğ’« â‚€ w'} {q q' : â—‡-Fam ğ’¬ w'}
        â†’ p â‰‹[ ğ’« ] p' â†’ q â—‡-â‰‹[ ğ’¬ ] q'
        â†’ (strength-fun p q) â—‡-â‰‹[ ğ’« Ã—' ğ’¬ ] (strength-fun p' q')
    strength-fun-pres-â‰‹ pâ‰‹p' (proof (refl , refl , qâ‰‹q')) = proof (refl , refl , proof (wk[ ğ’« ]-pres-â‰‹ _ pâ‰‹p' , qâ‰‹q'))

    strength-fun-natural : (i : w âŠ† w') (p : ğ’« â‚€ w) (q : â—‡-Fam ğ’¬ w)
      â†’  wk[ â—‡ (ğ’« Ã—' ğ’¬) ] i (strength-fun p q) â‰‹[ â—‡ (ğ’« Ã—' ğ’¬) ] strength-fun (wk[ ğ’« ] i p) (wk[ â—‡ ğ’¬ ] i q)
    strength-fun-natural w p q = let open EqReasoning â‰‹[ ğ’« ]-setoid in
      proof (refl , (refl , (proof
        ((begin
          wk[ ğ’« ] (factorâŠ† w _) (wk[ ğ’« ] (R-to-âŠ† _) p)
            â‰ˆË˜âŸ¨ wk[ ğ’« ]-pres-trans (R-to-âŠ† _) (factorâŠ† w _) p âŸ©
          wk[ ğ’« ] (âŠ†-trans (R-to-âŠ† _) (factorâŠ† w _)) p
            â‰¡Ë˜âŸ¨ cong (Î» w â†’ wk[ ğ’« ] w p) (factor-pres-R-to-âŠ† w _) âŸ©
          wk[ ğ’« ] (âŠ†-trans w (R-to-âŠ† (factorR w _))) p
            â‰ˆâŸ¨  wk[ ğ’« ]-pres-trans w (R-to-âŠ† (factorR w _)) p âŸ©
          wk[ ğ’« ] (R-to-âŠ† (factorR w _)) (wk[ ğ’« ] w p)
           âˆ)
        , â‰‹[ ğ’¬ ]-refl))))

opaque
  unfolding â—‡-map_
  
  strength-naturalâ‚ : (t : ğ’« â†’Ì‡ ğ’«') â†’ strength[ ğ’«' , ğ’¬ ] âˆ˜ (t Ã—'-map id') â‰ˆÌ‡ (â—‡-map (t Ã—'-map id')) âˆ˜ strength[ ğ’« , ğ’¬ ]
  strength-naturalâ‚ {ğ’¬ = ğ’¬} t = record { proof = Î» _p â†’ proof (refl , refl , proof (t .natural _ _ , â‰‹[ ğ’¬ ]-refl)) }

  strength-naturalâ‚‚ : (t : ğ’¬ â†’Ì‡ ğ’¬') â†’ strength[ ğ’« , ğ’¬' ] âˆ˜ (id' Ã—'-map (â—‡-map t)) â‰ˆÌ‡ (â—‡-map (id' Ã—'-map t)) âˆ˜ strength[ ğ’« , ğ’¬ ]
  strength-naturalâ‚‚ {ğ’¬' = ğ’¬'} {ğ’« = ğ’«} t = record { proof = Î» _p â†’ proof (refl , refl , â‰‹[ ğ’« Ã—' ğ’¬' ]-refl) }

  strength-assoc : â—‡-map assoc' âˆ˜ strength[ ğ’« Ã—' ğ’¬  , â„› ] â‰ˆÌ‡ (strength[ ğ’« , ğ’¬ Ã—' â„› ] âˆ˜ (id' Ã—'-map strength[ ğ’¬ , â„› ]) âˆ˜ assoc')
  strength-assoc {ğ’« = ğ’«} {ğ’¬ = ğ’¬} {â„› = â„›} = record { proof = Î» _p â†’ â‰‹[ â—‡ (ğ’« Ã—' (ğ’¬ Ã—' â„›)) ]-refl }

  strength-unit : â—‡-map Ï€â‚‚' âˆ˜ strength[ âŠ¤' , ğ’« ] â‰ˆÌ‡ Ï€â‚‚'
  strength-unit {ğ’«} = record { proof = Î» _p â†’ â‰‹[ â—‡ ğ’« ]-refl }
